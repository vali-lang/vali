
header "sys" as sys

use mem
use io
use os

class Poll[T] {
    - epoll_fd: i32 (0)
    - epoll_buffer: ptr
    - listeners: Array[Listener[T]] (Array[Listener[T]].new())
    - lsn_by_index: ptr
    - lsn_by_index_size: uint (20)

    static fn new() CLASS !poll_create_err {

        #if OS == linux
        let fd = sys:epoll_create(1)
        if fd < 0 {
            throw poll_create_err
        }
        let poll = CLASS {
            epoll_fd: fd
            epoll_buffer: mem:alloc(40 * sizeof(inline sys:libc_epoll_event))
            lsn_by_index: null
        }
        #else
        let poll = CLASS {
            epoll_buffer: mem:alloc(20 * sizeof(inline sys:libc_poll_item)),
            lsn_by_index: mem:alloc(20 * sizeof(ptr)),
        }
        #end
        return poll
    }

    fn wait(timeout: i32 (-1), buffer: ?Array[Listener[T]] (null)) Array[Listener[T]] {

        let events = isset(buffer) ? buffer : Array[Listener[T]].new()

        #if OS == linux
        let ebuf = this.epoll_buffer
        let count_ = sys:epoll_wait(this.epoll_fd, ebuf, 10, timeout)
        if count_ <= 0 {
            os:sleep_ms(1)
            return events
        }
        let count = count_ @as uint
        let i : uint = 0
        while i < count {
            let ev = (ebuf + (i * sizeof(inline sys:libc_epoll_event))) @as sys:libc_epoll_event
            let lsn = ev.data @as Listener[T]
            lsn.set_events(ev.events)
            if lsn.has_stopped_the_connection() {
                this.listeners.remove_item(lsn)
            }
            events.push(lsn)
            i++
        }
        #else
        let by_index = this.lsn_by_index
        let buf = this.epoll_buffer
        let i : uint = 0
        each this.listeners as lsn {
            let item = (buf + (i * sizeof(inline sys:libc_poll_item))) @as sys:libc_poll_item
            #if OS == macos
            item.fd = lsn.fd @as i32
            #else
            item.fd = lsn.fd
            #end
            item.events = lsn.detect_events @as i16
            item.revents = 0
            @ptrv(by_index, ptr, i) = lsn @as ptr
            i++
        }
        let all = i
        let count_ = sys:poll(buf, all @as u32, timeout)
        if count_ <= 0 {
            //let err = sys:WSAGetLastError()
            os:sleep_ms(10)
            return events
        }
        let count = count_ @as uint
        i = 0
        while i < all {
            let item = (buf + (i * sizeof(inline sys:libc_poll_item))) @as sys:libc_poll_item
            if item.revents == 0 {
                i++
                continue
            }
            let lsn = @ptrv(by_index, ptr, i) @as Listener[T]
            lsn.set_events(item.revents @as u32)
            if lsn.has_stopped_the_connection() {
                this.listeners.remove_item(lsn)
            }
            events.push(lsn)
            i++
        }
        #end

        return events
    }

    fn create_listener(fd: uint, data: T) Listener[T] {
        let lsn = Listener[T] {
            epoll_fd: this.epoll_fd
            fd: fd
            data: data
        }

        #if OS == linux
        // let ev = this.event_buf
        let ev = @stack(sys:libc_epoll_event)
        ev.events = 0
        ev.data = lsn @as ptr
        let res = sys:epoll_ctl(this.epoll_fd, sys:EPOLL_CTL_ADD, fd @as i32, ev)
        if res != 0 {
            // println("> Failed to add epoll listener")
        }
        #else
        // Increase size
        let size = this.lsn_by_index_size
        if this.listeners.length == size {
            size *= 2
            this.lsn_by_index_size = size
            mem:free(this.lsn_by_index)
            mem:free(this.epoll_buffer)
            this.lsn_by_index = mem:alloc(size * sizeof(ptr))
            this.epoll_buffer = mem:alloc(size * sizeof(inline sys:libc_poll_item))
        }
        #end

        lsn.update_epoll()
        this.listeners.push(lsn)

        return lsn
    }
}

class Listener[T] {
    data: T
    ~ epoll_fd: i32 (0)
    ~ fd: FD
    ~ detect_read: bool (false)
    ~ detect_write: bool (false)
    - detect_events: u32 (0)
    - events: u32 (0)

    fn set_events(events: u32) {
        this.events = events
    }

    fn has_read_request() bool {
        #if OS == linux
        return (this.events & sys:EPOLLIN) > 0
        #else
        return (this.events & sys:POLLIN) > 0
        #end
    }
    fn has_write_request() bool {
        #if OS == linux
        return (this.events & sys:EPOLLOUT) > 0
        #else
        return (this.events & sys:POLLOUT) > 0
        #end
    }
    fn has_stopped_the_connection() bool {
        let evs = this.events
        #if OS == linux
        return (evs & sys:EPOLLRDHUP) > 0 || (evs & sys:EPOLLHUP) > 0 || (evs & sys:EPOLLERR) > 0
        #elif OS == macos
        return (evs & sys:POLLRDHUP) > 0 || (evs & sys:POLLHUP) > 0 || (evs & sys:POLLERR) > 0
        #else
        return (evs & sys:POLLHUP) > 0 || (evs & sys:POLLERR) > 0
        #end
    }

    fn detect_read_request(v: bool) {
        this.detect_read = v
        this.update_epoll()
    }
    fn detect_write_request(v: bool) {
        this.detect_write = v
        this.update_epoll()
    }
    fn update_epoll() {
        let ev : u32 = 0
        if this.detect_read {
            #if OS == linux
            ev = ev | sys:EPOLLIN
            #else
            ev = ev | sys:POLLIN
            #end
        }
        if this.detect_write {
            #if OS == linux
            ev = ev | sys:EPOLLOUT
            #else
            ev = ev | sys:POLLOUT
            #end
        }
        #if OS == linux
        ev = ev | sys:EPOLLRDHUP
        #elif OS == win
        //ev = ev | sys:POLLHUP
        #else
        ev = ev | sys:POLLRDHUP
        #end

        this.detect_events = ev

        // let evo = this.event_buf
        // evo.events = this.detect_events
        // evo.data = this

        // let res = sys:epoll_ctl(this.epoll_fd, sys:EPOLL_CTL_MOD, this.fd, evo)

        #if OS == linux
        let evo = @stack(sys:libc_epoll_event)
        evo.events = this.detect_events
        evo.data = this
        let res = sys:epoll_ctl(this.epoll_fd, sys:EPOLL_CTL_MOD, this.fd @as i32, evo)
        #end
    }
}

