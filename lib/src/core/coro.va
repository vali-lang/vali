
header "sys" as sys;

use mem
use io

- global coro_run_first : ?Coro2 (null)
- global coro_run_last : ?Coro2 (null)
- global current_coro : @ignu(Coro2)

- global coro_poll : ?io:SimplePoll[Coro2] (null)
- global g_coros : @ignu(Array[?Coro2])
- global g_coro_indexes : @ignu(Pool[uint])
- global g_coro_poll_count : uint
- global g_coro_count : uint

struct Coro2 {
    args: ptr
    gc_stack: mem:StackPos
    result: ptr
    start_func: fn(Coro2)()
    handler: fn()()
    follow_up: ?Coro2 (null)
    next_resume: ?Coro2 (null)
    g_list_index: uint (0)

    s_stack: ?ptr (null)
    s_top: ptr (0 @as ptr)
    s_bottom: ptr (0 @as ptr)
    s_cont: ptr (0 @as ptr)
    s_size: uint (0)
    s_back_buf: ptr (0 @as ptr)

    test: uint (10)
    error: u32 (0)
    started: bool (false)
    done: bool (false)

    static fn new(handler: ptr, start_func: ptr, result_is_gc: bool) SELF {

        let gc_stack = mem:create_stack()
        let args = gc_stack.base + gc_stack.size - 128

        let coro = Coro2 {
            args: args
            gc_stack: gc_stack
            result: result_is_gc ? gc_stack.base : args
            //
            start_func: start_func @as fn(Coro2)()
            handler: handler @as fn()()
        }

        let indexes = g_coro_indexes
        if indexes.count == 0 {
            let index = g_coros.push(coro)
            coro.g_list_index = index
        } else {
            let index = indexes.get() ? 0
            g_coros.set(index, coro) _
            coro.g_list_index = index
        }

        let last = coro_run_last
        if isset(last) {
            last.next_resume = coro
            coro_run_last = coro
        } else {
            coro_run_first = coro
            coro_run_last = coro
        }

        g_coro_count++

        return coro
    }

    static fn get_poll() io:SimplePoll[Coro2] {
        let poll = coro_poll
        if poll == null {
            let p = io:SimplePoll[Coro2].new()
            coro_poll = p
            return p
        }
        return poll @as io:SimplePoll[Coro2]
    }

    static fn yield_current() {
        let current = current_coro
        if current != null {
            await_last_coro
        }
    }

    //////////////
    // AWAIT
    //////////////

    fn await_coro(coro: Coro2) {
        let ob = coro
        let ref = ob.follow_up
        while isset(ref) {
            ob = ref
            ref = ref.follow_up
        }
        ob.follow_up = this
        this.block()
    }

    fn await_fd(fd: FD, read: bool, write: bool) {
        let poll = Coro2.get_poll()
        poll.set(fd, this, read, write)
        g_coro_poll_count++
        this.block()
    }

    fn await_last_coro() {
        let last = coro_run_last
        if isset(last) {
            last.next_resume = this
            coro_run_last = this
        } else {
            coro_run_first = this
            coro_run_last = this
        }
        this.block()
    }

    //////////////
    // FLOW
    //////////////

    fn block() {
        // let here = @stack(inline [ptr, 5])
        current_coro = null
        let here = @stack(inline sys:libc_jmp_buf)
        if @setjmp(here) == 0 {
            this.s_cont = here @as ptr
            this.s_top = this.s_cont - 32

            #if OS == win
            @ptrv(here, ptr, 0) += 0x80 @as ptr
            #end

            // Copy stack
            let size = (this.s_bottom - this.s_top) @as uint
            let stack = this.s_stack
            let has_stack = isset(stack)
            let create_stack = !has_stack || size > this.s_size
            if create_stack {
                if has_stack : mem:free(stack @as ptr)
                stack = mem:alloc(size)
                this.s_size = size
                this.s_stack = stack
            }

            mem:copy(this.s_top, stack @as ptr, size)

            // Jump out
            @longjmp(this.s_back_buf)
        }
    }

    fn start() {
        let pad = @stack(inline [u8, 2048]) @as ptr
        this.started = true
        this.s_bottom = pad
        let start = this.start_func
        start(this)
    }
    fn continue() {
        let size = (this.s_bottom - this.s_top) @as uint
        let stack = this.s_stack @as ptr
        mem:copy(stack, this.s_top, size);
        @longjmp(this.s_cont)
    }

    static fn setjmp(buf: ptr) i32 {
        if @setjmp(buf) == 0 {
            return 0
        }
        return 1
    }
    fn resume() {
        if this.done : return
        current_coro = this

        let jmpbuf = @stack(inline sys:libc_jmp_buf)
        let current_stack = mem:stack_pos
        if @setjmp(jmpbuf) == 0 {

            #if OS == win
            @ptrv(jmpbuf, ptr, 0) += 0x80 @as ptr
            #end

            this.s_back_buf = jmpbuf
            mem:stack_pos = this.gc_stack
            if this.started == false {
                this.start()
            } else {
                this.continue()
            }
        }
        mem:stack_pos = current_stack

        if this.done {
            let next = this.follow_up
            if isset(next) {
                this.follow_up = null
                next.resume()
            }
        }
    }

    fn complete() {
        this.done = true
        current_coro = null
        g_coro_count--
        let index = this.g_list_index
        g_coros.set(index, null) _
        g_coro_indexes.add(index)
        @longjmp(this.s_back_buf)
    }

    //////////////
    // LOOP
    //////////////

    static fn loop() {
        let stack = mem:stack
        let poll = Coro2.get_poll()
        while g_coro_count > 0 {
            // Run coro chain
            let coro = coro_run_first
            while isset(coro) {
                coro.resume()
                let next = coro.next_resume
                coro.next_resume = null
                coro = next
            }
            coro_run_first = null
            coro_run_last = null
            // Wait for poll coros
            if g_coro_poll_count > 0 {
                let coros = poll.wait(1000) ! {
                    panic("Too many file descriptors")
                }
                if coros.length == 0 && stack.run_gc {
                    stack.gc()
                }
                g_coro_poll_count -= coros.length
                each coros as coro {
                    coro.resume()
                }
            }
        }
    }

    ///////////////////
    // GC
    ///////////////////

    -pkg static fn transfer_check() {
        let coros = g_coros

        each coros as coro {
            if isset(coro) {
                let base = coro.gc_stack.base
                let adr = coro.gc_stack.adr
                while adr > base {
                    adr -= sizeof(ptr)
                    mem:gc_transfer_item(@ptrv(adr, ptr, 0))
                }
            }
        }
    }

    fn _gc_mark() {
        let base = this.gc_stack.base
        let adr = this.gc_stack.adr
        while adr > base {
            adr -= sizeof(ptr)
            mem:gc_mark_item(@ptrv(adr, ptr, 0))
        }
    }
    fn _gc_mark_shared() {
        let base = this.gc_stack.base
        let adr = this.gc_stack.adr
        while adr > base {
            adr -= sizeof(ptr)
            mem:gc_mark_shared_item(@ptrv(adr, ptr, 0))
        }
    }

    fn _gc_free() {
        this.gc_stack.free()
        let s = this.s_stack
        if isset(s) {
            mem:free(s)
        }
    }
}
