
global pools : ptr (null)
global pool_count : uint
global pool_usage : uint

use io
use os

struct GcPool {
    size: uint
    first : ptr
    current : ptr
    index : uint (0)
    items_used: uint (0)
    items_total: uint
    items_total_last: uint
    state_used: u8 (0)
    items_use_gc_free: bool

    static fn init() {
        pools = alloc(50 * sizeof(ptr))
        let size : uint = 8
        let index : uint = 0
        while size <= 64 {
            @ptrv(pools, ptr, index++) = CLASS.new(size, false)
            @ptrv(pools, ptr, index++) = CLASS.new(size, true)
            size += 8
        }
        @ptrv(pools, ptr, index++) = CLASS.new(128, false)
        @ptrv(pools, ptr, index++) = CLASS.new(128, true)
        @ptrv(pools, ptr, index++) = CLASS.new(256, false)
        @ptrv(pools, ptr, index++) = CLASS.new(256, true)
        @ptrv(pools, ptr, index++) = CLASS.new(512, false)
        @ptrv(pools, ptr, index++) = CLASS.new(512, true)

        pool_count = index
    }
    static fn new(size_: uint, items_use_gc_free: bool) CLASS {
        let size = size_ + 8
        let block = GcPool.create_block(size, 10)
        let pool = CLASS{
            size: size
            first: block
            current: block
            items_total: @ptrv(block, uint, 1)
            items_total_last: @ptrv(block, uint, 1)
            items_use_gc_free: items_use_gc_free
        }
        if items_use_gc_free {
            pool.state_used = state_used
        }
        return pool
    }

    // ptr next, ptr itemc, ptr transferc, {...items...}
    static fn create_block(size: uint, itemc: uint) ptr {
        if itemc > 250 {
            itemc = 250
        }
        let fsize = itemc * size + 3 * sizeof(ptr)
        let block = alloc(fsize)
        clear(block, fsize)
        // @ptrv(block, ptr, 0) = null
        @ptrv(block, uint, 1) = itemc
        // @ptrv(block, uint, 2) = 0
        return block
    }

    fn get_next_block_or_create(current: ptr) ptr {

        let next = @ptrv(current, ptr)
        if next == null {
            next = GcPool.create_block(this.size, this.items_total * 2)
            this.items_total += @ptrv(next, uint, 1)
            @ptrv(current, ptr) = next
        }

        let usage = this.items_used * 100 / this.items_total_last

        if usage > pool_usage {
            pool_usage = usage
            if usage > 80 {
                stack.run_gc = true
                stack.bumper_overflow = true
            }
        }

        return next
    }

    fn get(vtable: u32) ptr {
        let size = this.size
        let block = this.current
        let index = this.index
        let itemc = @ptrv(block, uint, 1) // Item count
        let tc = @ptrv(block, uint, 2) // Transfer count
        while tc > itemc * 7 / 10 {
            block = this.get_next_block_or_create(block)
            index = 0
            itemc = @ptrv(block, uint, 1)
            tc = @ptrv(block, uint, 2)
            this.current = block
        } 
        //
        let base = block + sizeof(ptr) * 3
        let item = base + (size * index)
        while @ptrv(item, u8) != 0 {
            index++
            if index == itemc {

                block = this.get_next_block_or_create(block)
                index = 0
                base = block + sizeof(ptr) * 3
                itemc = @ptrv(block, uint, 1)
                this.current = block

                let tc = @ptrv(block, uint, 2)
                while tc > itemc * 7 / 10 {
                    block = this.get_next_block_or_create(block)
                    index = 0
                    itemc = @ptrv(block, uint, 1)
                    tc = @ptrv(block, uint, 2)
                    this.current = block
                    base = block + sizeof(ptr) * 3
                } 
            }
            item = base + (size * index)
        }
        let res = item
        let new_index = index + 1
        this.index = new_index
        if new_index == itemc {
            this.current = this.get_next_block_or_create(block)
            this.index = 0
        }
        @ptrv(res, u8, 0) = this.state_used
        @ptrv(res, u8, 1) = 0
        @ptrv(res, u8, 2) = 0
        @ptrv(res, u8, 3) = index @as u8
        @ptrv(res, u32, 1) = vtable

        this.items_used++

        return res + 8
    }

    static fn free_unused_all() {
        let pc = pool_count
        while pc-- > 0 {
            let pool = @ptrv(pools, GcPool, pc)
            if pool.items_use_gc_free {
                pool.free_unused()
            }
        }
    }
    fn free_unused() {
        let current = this.current
        let block = this.first
        let size = this.size
        while block != null {
            let b = block
            let itemc_ = @ptrv(b, uint, 1)
            if b == current {
                itemc_ = this.index
            }
            let itemc = itemc_
            let base = b + sizeof(ptr) * 3
            let i : uint = 0
            while i < itemc {
                let item = base + (size * i++)
                let state = @ptrv(item, u8, 0)

                if state == state_used {
                    let vtable = @ptrv(item, u32, 1)
                    let table = @gc_get_vtable(vtable)
                    let func = @ptrv(table, ?fn(ptr)(void), 2)
                    func(item + 8);

                    @ptrv(item, u8, 0) = state_unused
                }
            }
            // 
            if b == current {
                break
            }
            // Next block
            block = @ptrv(b, ptr)
        }
    }

    static fn reset_all() {
        let pc = pool_count
        while pc-- > 0 {
            let pool = @ptrv(pools, GcPool, pc)
            pool.reset()
        }
    }
    fn reset() {
        // Clear all un-used blocks after current
        let block = this.current
        let prev = block
        let next = @ptrv(block, ptr)
        while next != null {
            let block = next
            next = @ptrv(block, ptr)
            if @ptrv(block, uint, 2) == 0 {
                // Delete block
                this.items_total -= @ptrv(block, uint, 1)
                free(block)
                @ptrv(prev, ptr) = next
            } else {
                // Keep block
                prev = block
            }
        }

        // Set current to first
        this.current = this.first
        this.index = 0
        this.items_used = 0
        this.items_total_last = this.items_total
        pool_usage = 0
    }

    static fn clean_old_all(age: u8) {
        let pc = pool_count
        while pc-- > 0 {
            let pool = @ptrv(pools, GcPool, pc)
            pool.clean_old_age(age)
        }
    }
    fn clean_old_age(age: u8) {
        let block = this.first
        let size = this.size
        let call_gc_free = this.items_use_gc_free
        while block != null {
            let b = block
            let itemc = @ptrv(b, uint, 1)
            let base = b + sizeof(ptr) * 3
            let removed_count : uint = 0 
            // Loop items
            while itemc-- > 0 {
                let item = base + (size * itemc)
                let state = @ptrv(item, u8, 0)
                if state != state_solid {
                    continue
                }
                if @ptrv(item, u8, 2) != age {
                    if call_gc_free {
                        let vtable = @ptrv(item, u32, 1)
                        let table = @gc_get_vtable(vtable)
                        let func = @ptrv(table, ?fn(ptr)(void), 2)
                        func(item + 8);
                    }
                    @ptrv(item, u8, 0) = state_unused
                    removed_count++
                }
            }
            // Adjust transfer count
            @ptrv(b, uint, 2) -= removed_count
            // Next block
            block = @ptrv(b, ptr)
        }
    }

    static fn delete_all() {
        let pc = pool_count
        while pc-- > 0 {
            let pool = @ptrv(pools, GcPool, pc)
            pool.delete()
        }
    }
    fn delete() {
        let block = this.first
        while block != null {
            let next = @ptrv(block, ptr)
            free(block)
            block = next
        }

        free(this)
    }
}

